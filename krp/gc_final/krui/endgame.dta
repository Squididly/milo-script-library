#define ENDGAME_SCREEN_HANDLERS
((auto_back FALSE)
   (status
      (default
         ((button select)
            (text select))))
   (enter
      {$this clear_vram}
      {endgame_exit.btn
         set_localized_text
         {mode_mgr exit_mode_text}}))
{new_kr_panel
   endgame
   (navigator
      (vertical endgame_new.btn endgame_repeat.btn endgame_exit.btn))
   (focus endgame_new.btn)
   (retain_last_focus FALSE)
   (enter
      {if_else
         {the_db is_medley}
         {do
            {endgame.roll set_text medley_over}
            {endgame_new.btn set_text new_medley}
            {endgame_repeat.btn set_text repeat_medley}}
         {do
            {endgame.roll set_text song_over}
            {endgame_new.btn set_text new_song}
            {endgame_repeat.btn set_text repeat_song}}})
   (SELECT_MSG
      {switch
         $component
         (endgame_new.btn
            {{ui screen PARAM_SCREEN}
               set_focus_param_panel
               param_selsong}
            {ui goto_screen PARAM_SCREEN})
         (endgame_repeat.btn
            {meta_music_stop}
            {ui goto_screen LOADING_SCREEN})
         (endgame_exit.btn
            {ui goto_screen exit_meta_mode})})}
{new_kr_screen
   endgame
   (panels bg status left endgame)
   (focus endgame)
   (char none)
   ENDGAME_SCREEN_HANDLERS}
{new_kr_panel
   endgame_failed
   (navigator
      (vertical endgame_restart.btn endgame_practice.btn endgame_change.btn endgame_exit.btn))
   (focus endgame_restart.btn)
   (retain_last_focus FALSE)
   (enter
      {if
         {the_db is_demo}
         {endgame_practice.btn set_state kDisabled}}
      {endgame.lbl set_text song_failed}
      {endgame_restart.btn set_text restart_song}
      {endgame_change.btn set_text new_song})}
{new_kr_screen
   endgame_failed
   (panels pause_bg status endgame_failed)
   (focus endgame_failed)
   ENDGAME_SCREEN_HANDLERS
   (SELECT_MSG
      {switch
         $component
         (endgame_restart.btn
            {ui goto_screen restart_game})
         (endgame_practice.btn
            {the_db set_short_play FALSE}
            {the_db set_practice_mode TRUE}
            {ui goto_screen restart_game_clean})
         (endgame_change.btn
            {ui goto_screen exit_game_change})
         (endgame_exit.btn
            {ui goto_screen exit_game_main})})}
{new_kr_panel
   endgame_failround
   (navigator
      (vertical endgame_failround_continue.btn))
   (focus endgame_failround_continue.btn)
   (enter
      {cond
         ({the_db is_medley}
            {endgame_failround_continue.btn set_text medley_failed})
         ({the_db is_duet}
            {endgame_failround_continue.btn set_text duet_failed})
         (TRUE
            {endgame_failround_continue.btn set_text song_failed})})
   (SELECT_MSG
      {switch
         $component
         (endgame_failround_continue.btn
            {ui goto_screen exit_game_round})})}
{new_kr_screen
   endgame_failround
   (panels pause_bg status endgame_failround)
   (focus endgame_failround)
   (auto_back FALSE)
   (char none)
   (status
      (default
         ((button select)
            (text continue))))}
{new_kr_panel
   endgame_practice
   (navigator
      (vertical endgame_perform.btn endgame_again.btn endgame_new.btn endgame_exit.btn))
   (focus endgame_perform.btn)
   (retain_last_focus FALSE)}
{new_kr_screen
   endgame_practice
   (panels pause_bg status endgame_practice)
   (focus endgame_practice)
   ENDGAME_SCREEN_HANDLERS
   (SELECT_MSG
      {switch
         $component
         (endgame_perform.btn
            {the_db set_short_play FALSE}
            {the_db set_practice_mode FALSE}
            {ui goto_screen restart_game_clean})
         (endgame_again.btn
            {ui goto_screen restart_game})
         (endgame_new.btn
            {ui goto_screen exit_game_change})
         (endgame_exit.btn
            {ui goto_screen exit_game_main})})}
{new_kr_panel
   endgame_tutorial
   (navigator
      (vertical endgame_again.btn endgame_exit.btn))
   (focus endgame_again.btn)
   (retain_last_focus FALSE)}
{new_kr_screen
   endgame_tutorial
   (panels pause_bg status endgame_tutorial)
   (focus endgame_tutorial)
   ENDGAME_SCREEN_HANDLERS
   (SELECT_MSG
      {switch
         $component
         (endgame_again.btn
            {ui goto_screen restart_game})
         (endgame_exit.btn
            {ui goto_screen exit_game_main})})}
{new_kr_panel
   endmedley_failed
   (navigator
      (vertical endgame_restart.btn endgame_change.btn endgame_exit.btn))
   (focus endgame_restart.btn)
   (retain_last_focus FALSE)}
{new_kr_screen
   endmedley_failed
   (panels pause_bg status endmedley_failed)
   (focus endmedley_failed)
   ENDGAME_SCREEN_HANDLERS
   (SELECT_MSG
      {switch
         $component
         (endgame_restart.btn
            {ui goto_screen restart_game})
         (endgame_change.btn
            {ui goto_screen exit_game_change})
         (endgame_exit.btn
            {ui goto_screen exit_game_main})})}